//@version=4
strategy(title="MACD + Divergences [Alorse]", shorttitle = "MACD + Divergences", pyramiding=0, currency=currency.USD, default_qty_type=strategy.percent_of_equity, initial_capital=1000, default_qty_value=10, commission_type=strategy.commission.percent, commission_value=0.01)

txtVer = "2.0.0"
version = input(title="Version", type=input.string, defval=txtVer, options=[txtVer], tooltip="This is informational only, nothing will change.")
src = input(title="Source", type=input.source, defval=close)
// Getting inputs
macdGroup = "MACD Settings"
fast_length = input(title="Length   Fast", type=input.integer, defval=12, group=macdGroup, inline="macdLength")
slow_length = input(title="Slow", type=input.integer, defval=26, group=macdGroup, inline="macdLength")
signal_length = input(title="Signal Smoothing", type=input.integer, minval = 1, maxval = 50, defval = 9, group=macdGroup)
sma_source = input(title="Oscillator", type=input.string, defval="EMA", options=["SMA", "EMA"], group=macdGroup, inline="macdMa", tooltip="Moving Average Type")
sma_signal = input(title="Signal Line", type=input.string, defval="EMA", options=["SMA", "EMA"], group=macdGroup, inline="macdMa", tooltip="Moving Average Type")
// Plot colors
col_macd = input(#2962FF, "MACD Line  ", input.color, group="Color Settings", inline="MACD")
col_signal = input(#FF6D00, "Signal Line  ", input.color, group="Color Settings", inline="Signal")
showCross = input(title="Show Signals&MACD Cross", defval=true, group="Color Settings") // Show cross bt MACD & Signal
col_grow_above = input(#00796b, "Above   Grow", input.color, group="Histogram", inline="Above")
col_fall_above = input(#26a69a, "Fall", input.color, group="Histogram", inline="Above")
col_grow_below = input(#ef5350, "Below Grow", input.color, group="Histogram", inline="Below")
col_fall_below = input(#b71c1c, "Fall", input.color, group="Histogram", inline="Below")
// Calculating
fast_ma = sma_source == "SMA" ? sma(src, fast_length) : ema(src, fast_length)
slow_ma = sma_source == "SMA" ? sma(src, slow_length) : ema(src, slow_length)
macd = fast_ma - slow_ma
signal = sma_signal == "SMA" ? sma(macd, signal_length) : ema(macd, signal_length)
hist = macd - signal
osc = macd

plot(hist, title="Histogram", style=plot.style_columns, color=color.new((hist>=0 ? (hist[1] < hist ? col_grow_above : col_fall_above) : (hist[1] < hist ? col_grow_below : col_fall_below) ),0))
plot(macd, title="MACD", color=color.new(col_macd, 10))
plot(signal, title="Signal", color=color.new(col_signal, 10))

donttouchzero = input(title="Don't touch the zero line?", defval=true)

//Divergences
lbR = input(title="Pivot Lookback Right", defval=2, group="Divergences")
lbL = input(title="Pivot Lookback Left", defval=7, group="Divergences")
rangeUpper = input(title="Max of Lookback Range", defval=200, group="Divergences")
rangeLower = input(title="Min of Lookback Range", defval=10, group="Divergences")
plotBull = input(title="Plot Bullish", defval=true, group="Divergences")
plotBear = input(title="Plot Bearish", defval=true, group="Divergences")

bearColor = color.red
bullColor = color.green
textColor = color.white
noneColor = color.new(color.white, 100)

plFound = na(pivotlow(osc, lbL, lbR)) ? false : true
phFound = na(pivothigh(osc, lbL, lbR)) ? false : true

_inRange(cond) =>
    bars = barssince(cond == true)
    rangeLower <= bars and bars <= rangeUpper

//Get MA Values
maA = input(150, minval=1, title="EMA 1", group="Moving Averages")
maB = input(600, minval=1, title="EMA 2", group="Moving Averages")
//Get MTF data
dataA = ema(src, maA)
dataB = ema(src, maB)

trendBull = dataA > dataB and osc[lbR] < 0
trendBear = dataA < dataB and osc[lbR] > 0

//------------------------------------------------------------------------------
// Regular Bullish

// Osc: Higher Low
oscHL = osc[lbR] > valuewhen(plFound, osc[lbR], 1) and _inRange(plFound[1])
priceHHZero =  highest(osc, lbL+lbR+5) 
blowzero = donttouchzero ? priceHHZero < 0 : true
// Price: Lower Low
priceLL = low[lbR] < valuewhen(plFound, low[lbR], 1)
bullCond = plotBull and priceLL and oscHL and plFound and blowzero
plot(plFound ? osc[lbR] : na, offset=-lbR, title="Regular Bullish", linewidth=2, color=(bullCond ? color.new(bullColor, trendBull ? 0 : 50) : noneColor))
plotshape(bullCond ? osc[lbR] : na, offset=-lbR, title="Regular Bullish Label", text="⬆", style=shape.labelup, location=location.absolute, color=color.new(bullColor,100), textcolor=color.new(bullColor, trendBull ? 0 : 50))

//------------------------------------------------------------------------------
// Regular Bearish

// Osc: Lower High
oscLH = osc[lbR] < valuewhen(phFound, osc[lbR], 1) and _inRange(phFound[1])
priceLLZero =  lowest(osc, lbL+lbR+5) 
bearzero = donttouchzero ? priceLLZero > 0 : true
// Price: Higher High
priceHH = high[lbR] > valuewhen(phFound, high[lbR], 1)
bearCond = plotBear and priceHH and oscLH and phFound and bearzero
// plot(phFound ? osc[lbR] : na, offset=-lbR, title="Regular Bearish", linewidth=2, color=(bearCond ? color.new(bearColor, trendBear ? 0 : 50) : noneColor))
// plotshape(bearCond ? osc[lbR] : na, offset=-lbR, title="Regular Bearish Label", text="⬇", style=shape.labeldown, location=location.absolute, color=color.new(bearColor,100), textcolor=color.new(bearColor, trendBear ? 0 : 50))
plot(phFound ? osc[lbR] : na, offset=-lbR, title="Regular Bearish", linewidth=2, color=(bearCond ? color.new(bearColor, trendBear ? 0 : 50) : noneColor))
plotshape(bearCond ? osc[lbR] : na, offset=-lbR, title="Regular Bearish Label", text="⬇", style=shape.labeldown, location=location.absolute, color=color.new(bearColor,100), textcolor=color.new(bearColor, trendBear ? 0 : 50))


plot(showCross and crossunder(osc, signal) and trendBear ? osc : na, style=plot.style_cross, color=color.red, linewidth = 3)
plot(showCross and crossover(osc, signal) and trendBull ? osc : na, style=plot.style_cross, color=color.green, linewidth = 3)

// Strategy
stratGroup = "Strategy"
barsBack = input(title="Bars back", type=input.integer, minval=0, maxval=100, defval=10, group=stratGroup, tooltip="")
tpFactor = input(title="Take profit factor", type=input.float, minval=0, maxval=100, defval=1.5, group=stratGroup, tooltip="")

buySignal = bullCond and trendBull
sellSignal = bearCond and trendBear
float slBuy = 0.0
slBuy := strategy.position_size == 0 ? lowest(barsBack) : na(slBuy[1]) ? na : slBuy[1]
float slSell = 0.0
slSell := strategy.position_size == 0 ? highest(barsBack) : na(slSell[1]) ? na : slSell[1]

entry_price = 0.0
entry_price := buySignal ? close : sellSignal ? close : na(entry_price[1]) ? na : entry_price[1]

tpBuy = entry_price + ((entry_price - slBuy) * tpFactor)
tpSell = entry_price - ((slSell - entry_price) * tpFactor)

// Stop Loss
slGroup = "Stop Loss"
useSL = input(true, title="╔══════   Enable   ══════╗", group=slGroup, tooltip="If you are using this strategy for Scalping or Futures market, we do not recommend using Stop Loss.")
SLbased = input(title="Based on", type=input.string, defval="Percent", options=["ATR", "Percent"], group=slGroup, tooltip="ATR: Average True Range\nPercent: eg. 5%.")
multiATR = input(7.0, title="ATR   Mult", type=input.float, group=slGroup, inline="atr")
lengthATR = input(14, title="Length", type=input.integer, group=slGroup, inline="atr")
SLPercent = input(7, title="Percent", type=input.float, group=slGroup) * 0.01
plotSL = input(true, title="Show SL", group=slGroup)

longStop = 0.0
shortStop = 0.0

if SLbased == "ATR"
    longStop := valuewhen(buySignal, low, 0) - (valuewhen(buySignal, rma(tr(true), lengthATR), 0) * multiATR)
    longStopPrev = nz(longStop[1], longStop)
    longStop := close[1] > longStopPrev ? max(longStop, longStopPrev) : longStop

    shortStop := (valuewhen(sellSignal, rma(tr(true), lengthATR), 0) * multiATR) + valuewhen(sellSignal, high, 0)
    shortStopPrev = nz(shortStop[1], shortStop)
    shortStop := close[1] > shortStopPrev ? max(shortStop, shortStopPrev) : shortStop
if SLbased == "Percent"
    longStop  := strategy.position_avg_price * (1 - SLPercent)
    shortStop := strategy.position_avg_price * (1 + SLPercent)


strategy.entry("Long", true, when=buySignal)
// strategy.close("Long", when=close > tpBuy)
strategy.exit("Exit", "Long", stop=longStop, limit=tpBuy)
strategy.entry("Short", false, when=sellSignal)
// strategy.close("Short", when=close < tpSell)
strategy.exit("Exit", "Short", stop=shortStop, limit=tpSell)
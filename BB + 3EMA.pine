//
//@author Alorse
//@version=4
study(title="Simple Bollinger Bands + 3 EMA/SMA [Alorse]", shorttitle="BB + 3 EMA/SMA [Alorse]", overlay=true)
txtVer = "1.0.2"
version = input(title="Version", type=input.string, defval=txtVer, options=[txtVer], tooltip="This is informational only, nothing will change.")

src = input(title="Source", type=input.source, defval=close)

// Bollinger Bands
bbGroup = "Bollindger Bands"
isBB = input(true, title="╔═════ Show Bollinger Bands ═════╗", group=bbGroup, tooltip="If you don't want to see Bollinger Bands")
length = input(20, title="Period Bollindger Bands", group=bbGroup)
mult = input(2.0, minval=0.001, maxval=5, group=bbGroup)

basis = sma(src, length)
dev = mult * stdev(src, length)
upper = basis + dev
lower = basis - dev
offset = input(0, "Offset", type = input.integer, minval = -500, maxval = 500)

plot(isBB ? basis : na, "Basis", color=color.new(color.orange, 75), offset = offset)
pb1 = plot(isBB ? upper : na, color=color.new(color.lime, 75), title="Upper")
pb2 = plot(isBB ? lower : na, color=color.new(color.lime, 75), title="Lower")
fill(pb1, pb2, color=color.new(color.lime, 99), title="Background")

//EMA
ma_type = input(title="Moving Average Type", type=input.string, defval="EMA", options=["EMA", "SMA"], group="Moving Averages")
lenFast = input(10, minval=1, title="Fast EMA", group="Moving Averages")
lenSlow = input(55, minval=1, title="Slow EMA", group="Moving Averages")
lenTend = input(200, minval=1, title="Tend EMA", group="Moving Averages")
showCross = input(title="Show MA Cross", defval=true, group="Moving Averages")

emaFast = ma_type == "EMA" ? ema(src, lenFast) : sma(src, lenFast)
emaSlow = ma_type == "EMA" ? ema(src, lenSlow) : sma(src, lenSlow)
emaTend = ma_type == "EMA" ? ema(src, lenTend) : sma(src, lenTend)

plot(emaFast, title="Fast MA", linewidth=1, color=color.new(color.blue, 10))
plot(emaSlow, title="Slow MA", linewidth=1, color=color.new(color.orange, 10))
plot(emaTend, title="Tend MA", linewidth=2, color=color.new(color.white, 10))

// Cross detect
cross_up = crossover(emaFast , emaSlow)
cross_down = crossunder(emaFast , emaSlow)
plot(showCross and cross_up or showCross and cross_down ? emaFast : na, style=plot.style_cross, title="Cross", linewidth=2, color=color.white)

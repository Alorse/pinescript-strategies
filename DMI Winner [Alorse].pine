//@version=4
strategy("Directional Movement Index Winner [Alorse]", shorttitle="DMI Winner [Alorse]", overlay=true, pyramiding=0, currency=currency.USD, default_qty_type=strategy.percent_of_equity, initial_capital=1000, default_qty_value=20, commission_type=strategy.commission.percent, commission_value=0.01) 

txtVer = "0.0.9"
version = input(title="Version", type=input.string, defval=txtVer, options=[txtVer], tooltip="This is informational only, nothing will change.\n\nEntry conditions:\n")
src = input(title="Source", type=input.source, defval=close)

// DMI
dmiGroup = "Directional Movement Index"
len = input(14, minval=1, title="DI Length", group=dmiGroup)
lensig = input(13, title="ADX Smoothing", minval=1, maxval=50, group=dmiGroup)
keyLevel = input(23, title="key level", group=dmiGroup)
[diplus,diminus,adx] = dmi(len, lensig)

// Moving Average
maGroup = "Moving Average"
hasMa = input(true, title="╔══════   Use MA   ══════╗", group=maGroup)
maType = input(title="Type", type=input.string, defval="EMA", options=["EMA", "SMA"], group=maGroup, inline="ma")
maLen = input(55, minval=1, title="Length", group=maGroup, inline="ma")
ma = maType == "EMA" ? ema(src, maLen) : sma(src, maLen)
plot(hasMa ? ma : na, title="Moving Average", linewidth=1, color=color.new(color.blue, 10))
buyMaFilter = hasMa ? close > ma : true
sellMaFilter = hasMa ? close < ma : true

// Strategy
stratGroup = "Strategy"
showLong = input(true, title="Long entries", group=stratGroup)
showShort = input(false, title="Short entries", group=stratGroup)

longCond = diplus < diminus and diplus[1] < diminus[1] and diplus[2] < diminus[2]
shortCond = diplus > diminus and diplus[1] > diminus[1] and diplus[2] > diminus[2]
longEntry = shortCond and adx > keyLevel and buyMaFilter
shortEntry = longCond and adx > keyLevel and sellMaFilter

if showLong and not showShort
    strategy.entry("Long", true, when=longEntry)
    strategy.close("Long", when=shortEntry)
if not showLong and showShort
    strategy.entry("Short", false, when=shortEntry)
    strategy.close("Short", when=longEntry)
if showLong and showShort
    strategy.entry("Long", true, when=longEntry)
    strategy.entry("Short", false, when=shortEntry)

// Stop Loss
slGroup = "Stop Loss"
useSL = input(false, title="╔══════   Enable   ══════╗", group=slGroup)
SLPercent = input(10, title="Percent", type=input.float, group=slGroup) * 0.01

longStop  = strategy.position_avg_price * (1 - SLPercent)
shortStop = strategy.position_avg_price * (1 + SLPercent)

if useSL
    strategy.exit("Stop Loss", "Long", stop=longStop)